#!/bin/zsh
SCRIPT_PATH=$0

# DEPENDENCIES: git-rstash 

help() {
  cat ${SCRIPT_PATH} | grep "^ *#"":"
}

#######################################################################
#                          Stacked Git Utils                          #
#######################################################################

stg-info() {
  #: stg-info - prints status and expanded information about the current stack.

  echo "## Status:"
  git -c color.status=always status -s \
    | sed 's:^:    :'

  echo

  echo "## Stack:"
  stg-pretty-series \
    | sed 's:^:    :'
}

stg-showstat() {
  #: stg-showstat - prints the modified files

  stg show -O "--name-status" "$@"
}

stg-list-patches() {
  #: stg-list-patches - prints undecorated list of patches
  stg series --noprefix --no-description "$@"
}

stg-stack-files() {
  git diff $(stg id $(stg series --noprefix | head -n 1)).. --name-status | cat
}

stg-series-pretty() {
  #: stg-series-pretty - prints expanded information about the current stack.

  local ARGS=()

  local _full=0
  local _extra=0
  local _notes=0
  local _help=0
  local _filter_set=0
  while [[ $# -gt 0 ]]; do
    if [[ $1 == (--full|-f) ]]; then
      _full=1
      shift
    elif [[ $1 == (--extra|-e) ]]; then
      _extra=1
      shift
    elif [[ $1 == (--notes|-n) ]]; then
      _notes=1
      shift
    elif [[ $1 == (--help|-h) ]]; then
      _help=1
      shift
    elif [[ $1 == (-a|-A|-U|-H) ]]; then
      _filter_set=1

      ARGS+=($1)
      shift
    else
      ARGS+=($1)
      shift
    fi
  done

  if [[ $_filter_set -eq 0 ]]; then
    ARGS=(-A)
  fi

  if [[ _help -eq 1 ]]; then
    stg series -h
    exit $?
  fi

  while read line; do
    # ZSH-NOTE: ( ${(@f)$( command )} ) splits command output by newline, returns array. (zsh)
    stg_fields=( "${(@f)$(
      echo $line | awk '{
        # stg_patch_status:
        print $1;

        # stg_patch_name_col:
        print substr($0, index($0,$2),index($0,$3) - index($0,$2));

        # stg_patch_description_col:
        print substr($0,index($0,$4));

        # stg_patch_name:
        print $2
      }'
    )}" )

    stg_patch_status=${stg_fields[1]}
    stg_patch_name_col=${stg_fields[2]}
    stg_patch_description_col=${stg_fields[3]}
    stg_patch_name=${stg_fields[4]}

    git_sha=$(stg id "$stg_patch_name" | xargs git rev-parse --short)

    COLOR=1
    if [[ $stg_patch_status == "+" ]]; then
      COLOR=2
    elif [[ $stg_patch_status == ">" ]]; then
      COLOR=3
    fi

    printf "%s$stg_patch_status $stg_patch_name_col%s # $stg_patch_description_col\n" $(tput setaf $COLOR) $(tput sgr0)

    details=$(
      if [[ $_full -eq 1 || $_extra -eq 1 ]]; then
        printf "%sgit-sha%s $git_sha\n\n" $(tput setaf 4) $(tput sgr0)
      fi

      if [[ $_full -eq 1 || $_extra -eq 1 ]]; then
        git_commit_msg=$(
          git show -s --format=%b "$git_sha" \
            | fold -w 80 -s \
            | perl -0pe 's:[\n\s]+$:\n:'
        )

        commit_prefix="$(tput setaf 1)#$(tput sgr0)"
        if [[ -n $git_commit_msg ]]; then
          echo "$git_commit_msg" \
            | awk -v prefix="$commit_prefix" '{print prefix " " $0}'
        else
          echo "$commit_prefix [ No Commit Summary ]"
        fi
        echo
      fi

      if [[ $_full -eq 1 || $_notes -eq 1 ]]; then
        git_notes=$(
          git notes show "$git_sha" 2>/dev/null \
            | perl -pe 's:(arc-revision|depends-on) :$1 D:' \
        )
        if [[ -n $git_notes ]]; then
          echo "$git_notes" \
            | perl -pe "s:^\\s*([^\s]+)\\s+(.*):$(tput setaf 4)\\1$(tput sgr0) \\2:" \
            | column -tx -s " "
          echo
        fi
      fi

      if [[ $_full -eq 1 ]]; then
        git show --name-status --format= "$git_sha" \
          | perl -pe "s:^(\\s*\\w+)(.*):$(tput setaf 2)\\1$(tput setaf 4)\\2$(tput sgr0):"
      fi
    )

    if [[ -n "$details" ]]; then
      result=$(
          printf "%s" "$details"
          printf "\n \n"
      )
      echo "$result" | sed 's:^:    :'
    fi
  done << EOF
$(stg series -d "${ARGS[@]}")
EOF
}

stg-bottom() {
  #: stg-bottom - get the sha of the first commit in the stack.
  stg ls -A | head -n 1 | xargs stg id
}


stg-gn() {
  #: stg-gn - go to next patch in the stack.

  stg goto "$@" "$(stg next)"
}

stg-gp() {
  #: stg-gp - go to previous patch in the stack.

  stg goto "$@" "$(stg prev)"
}

stg-ctx() {
  #: stg-ctx - interactively switch stacked git branch.

  stg-branches | fzf "$@" --preview 'stg series -b {}' | xargs git switch
}

stg-branches() {
  git branch | cut -c 3- | grep .stgit | sed s:.stgit::
}

stg-rstash() {
  #: stg-rstash - interactively switch stacked git branch.

  git rstash "$@" -- "HEAD:$(stg top)";
}

stg-refs() {
  local REFS=()
  for i in $(seq 0 $(stg series |wc -l)); do
    REFS+=("HEAD~$i")
  done
  if [[ $1 == "-n" ]]; then
    echo "${REFS[@]}"
  else
    printf "%s\n" "${REFS[@]}"
  fi
}

stg-overview() {
  stg-branches | xargs -I {} sh -c 'echo "::::: {}" && stg series -b {} && echo'
}

#######################################################################
#                            Git Utilites                             #
#######################################################################

git-ctx() {
  git-branch-log 100 \
  | grep -v ".stgit" \
  | fzf +s "$@" --preview='git-utils git-quick-log -n 5 {}' --height "100%" --preview-window="right:60%" \
  | xargs git switch
}

git-branch-history() {
  local __max_num=${1:-20}

  git-branch-history-list ${__max_num} \
    | awk '{print $2 "%%" "<-" "%%" $1}' \
    | column -tx -s "%%" \
    | less
}

git-branch-log() {
  local __max_num=${1:-20}

  git-branch-history-list ${__max_num} \
    | awk '{print $2}' \
    | awk '!x[$0]++'
}

git-branch-history-list() {
  local __max_num=$1

  git reflog \
    | grep --line-buffered 'checkout: moving from' \
    | perl -pe 's/.*moving from (.+) to (.+)/$1 $2/'  \
    | head -n ${__max_num}
}

git-quick-log() {
  git log -n 3 --format="%h %al (%ar)%n  %s" "$@"
}

git-browse-url() {
  local __file_path=$1
  local __line_number=$2

  # local __browse_url_tpl=$(git config browse.custom_cmd)

  # if $(git config browse.service) == "sourcegraph"
  # __url_domain=$(git config browse.domain)
  # __url_repo=$(git config browse.repo)
  # __url_tpl=$(git config browse.custom)

  # domain: "https://sourcegraph.com"
  # repo:   "some-repo-domain/some-repo"

  # echo "${__url_domain}/${__url_repo}/-/blob/${__file_path}#${__line_number}"
}

#######################################################################
#                           FARC Extensions                           #
#######################################################################

farc-quick() {
  local ARGS=()
  while [[ $# -gt 0 ]]; do
    ARGS+=($1)
    shift
  done
  farc "${ARGS[@]}"
}

stg-farc() {
  stg-refs | farc-bulk --stdin --fzf $@
}

farc-bulk() {
  local FARC_CMD=$1; shift
  local REFS=()
  local ARGS=()

  local _fzf=0
  local _rest=0
  local _stdin=0
  while [[ $# -gt 0 ]]; do
    if [[ $1 == '--' ]]; then
      _rest=1
      shift
    elif [[ $1 == '--fzf' ]]; then
      _fzf=1
      shift
    elif [[ $1 == '--stdin' ]]; then
      _stdin=1
      shift
    elif [[ _rest -eq 1 ]]; then
      REFS+=($1)
      shift
    else
      ARGS+=($1)
      shift
    fi
  done

  if [[ _stdin -eq 1 ]]; then
    while IFS="" read -r REF || [ -n "$REF" ]; do
      REFS+=($REF)
    done
  fi

  if [[ ${#REFS[@]} -lt 1 ]]; then
    REFS+=(HEAD)
  fi

  local RESULTS=$(printf "%s\n" "${REFS[@]}")
  if [[ _fzf -eq 1 ]]; then
    RESULTS=$(echo "$RESULTS" | fzf -m --preview="git show --oneline --name-status {}")
  fi

  echo -n "$RESULTS" | xargs -p -I %REF% echo farc "$FARC_CMD" "%REF%" "${ARGS[@]}"
}

#######################################################################

if [[ "$1" == (-h|--help|-?) ]]; then
  help
  exit 125
fi
"$@"
